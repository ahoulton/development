on load:
	set {whitelist} to false if {whitelist} isn't set
	set {whitelist::message} to "&cWe are under maintenance, we're sorry for any inconvenience that this may cause." if {whitelist::message} isn't set

on connect:
	{whitelist} is true
	player doesn't have permission "whitelist.bypass"
	kick player due to "%{whitelist::message}%"
	
command /whitelist <boolean>:
	permission: staff.whitelist
	usage: &cCorrect usage: /whitelist <true/false>
	trigger:
		if arg 1 is true:
			if {whitelist} is true:
				send "&cWhitelist is already enabled, nothing has been changed." to sender
				stop
			set {whitelist} to true
			send "&aEnabled &7whitelist."
		else if arg 1 is false:
			if {whitelist} is false:
				send "&cWhitelist is already disabled, nothing has been changed." to sender
				stop
			set {whitelist} to false
			send "&cDisabled &7whitelist."

command /whitelist:setwlmsg [<text>]:
	permission: staff.whitelist.setmsg
	trigger:
		if arg 1 isn't set:
			send "&7Whitelist message: &f%{whitelist::message}%" to sender
			stop
		set {whitelist::message} to colored arg-1
		send "&7Whitelist message: &f%{whitelist::message}%" to sender

command /whitelist:kickall:
	permission: staff.whitelist.kickall
	trigger:
		kick all players due to "%{whitelist::message}%"